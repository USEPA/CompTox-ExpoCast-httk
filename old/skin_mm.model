States = {
Agutlumen;
Agut;
Aliver;
Aven;
Alung;
Aart;
Arest;
Akidney;
Atubules;
Ametabolized;
AUC;
Askinexposed;
Askin;
Amedia;
};

Inputs = {forcing;switch;};

Outputs = {
Cgut;
Cliver;
Cven;
Clung;
Cart;
Crest;
Ckidney;
Cplasma;
Aplasma;
Cskinexposed;
Cskin;
Cmedia;
};
depth = 0;
V0 = 0;
Fskinexposed = 0;
totalSA = 0;
SA = 0;
Kp = 0;
Kskin2media = 0;
BW = 70;
Vmax = 0;
Km = 1;
hematocrit = 0.44;
kgutabs = 1;
Kkidney2pu = 0;
Kliver2pu = 0;
Krest2pu = 0;
Kgut2pu = 0;
Klung2pu = 0;
Kskin2pu = 0;
Qcardiacc = 4.8;
Qgfrc = 0.108;
Qskinf = 0;
Qskin = 0;
Qskinexposed = 0;
Qgutf = 0;
Qkidneyf = 0;
Qliverf = 0;
Vartc = 0;
Vgutc = 0;
Vkidneyc = 0;
Vliverc = 0;
Vlungc = 0;
Vrestc = 0;
Vvenc = 0;
Vskinc = 0;
Vskin = 0;
Vskinexposed = 0;
Fraction_unbound_plasma = 0.0;
Rblood2plasma = 0.0;
Qcardiac = 0.0;
Qgfr = 0.0;
Qgut = 0.0;
Qkidney = 0.0;
Qliver = 0.0;
Qrest = 0.0;
Vart = 0.0;
Vgut = 0.0;
Vkidney = 0.0;
Vliver = 0.0;
Vlung = 0.0;
Vrest = 0.0;
Vven = 0.0;



Initialize{
kgutabs = kgutabs * 24;
Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
Qgfr = Qgfrc * pow(BW, 0.75) * 24;
Qgut = Qcardiac * Qgutf;
Qkidney = Qcardiac * Qkidneyf;
Qliver = Qcardiac * Qliverf;
SA = Fskinexposed * totalSA;
Vskinexposed = SA * depth * 0.001;
Vskin = Vskinc * BW - Vskinexposed;
Qskin = Qcardiac * Qskinf * (1 - Vskinexposed / (Vskinc * BW));
Qskinexposed = Qcardiac * Qskinf * Vskinexposed / (Vskinc * BW);
Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qskin + Qskinexposed);
Vart = Vartc * BW;
Vgut = Vgutc * BW;
Vkidney = Vkidneyc * BW;
Vliver = Vliverc * BW;
Vlung = Vlungc * BW;
Vrest = Vrestc * BW;
Vven = Vvenc * BW;
}

Dynamics{
Cgut = Agut / Vgut;
Cliver = Aliver / Vliver;
Cven = Aven / Vven;
Clung = Alung / Vlung;
Cart = Aart / Vart;
Crest = Arest / Vrest;
Ckidney = Akidney / Vkidney;
Cplasma = Aven / Vven / Rblood2plasma;
Aplasma = Aven / Rblood2plasma * (1 - hematocrit);
Cskin = Askin / Vskin;
Cskinexposed = Askinexposed / Vskinexposed;
Vmedia = V0 * forcing;
Cmedia = Amedia / Vmedia;

 

dt (Agutlumen) = - kgutabs * Agutlumen;
dt (Amedia) = - Kp * SA * 24 * 0.001 * (Cmedia - Cskinexposed / Kskin2media) * switch;
dt (Agut) = kgutabs * Agutlumen + Qgut * (Aart / Vart - Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
dt (Aliver) = Qliver * Aart / Vart + Qgut * Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Aliver / Vliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Vmax * 24 * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu);
dt (Aven) = ((Qliver + Qgut) * Aliver / Vliver / Kliver2pu + Qkidney * Akidney / Vkidney / Kkidney2pu + Qrest * Arest / Vrest / Krest2pu + Qskin * Cskin / Kskin2pu + Qskinexposed * Cskinexposed / Kskin2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Aven / Vven;
dt (Alung) = Qcardiac * (Aven / Vven - Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
dt (Aart) = Qcardiac * (Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma - Aart / Vart );
dt (Arest) = Qrest * (Aart/Vart - Arest/Vrest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
dt (Akidney) = Qkidney * Aart / Vart - Qkidney * Akidney / Vkidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Akidney / Vkidney / Kkidney2pu;
dt (Atubules) = Qgfr * Akidney / Vkidney / Kkidney2pu;
dt (Ametabolized) = Vmax * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu) ;
dt (AUC) = Aven / Vven / Rblood2plasma;
dt (Askinexposed) = Qskinexposed * (Cart - Cskinexposed * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma) + Kp * SA * 24 * 0.001 * (Cmedia - Cskinexposed / Kskin2media) * switch;
dt (Askin) = Qskin * (Cart - Cskin * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma);
}