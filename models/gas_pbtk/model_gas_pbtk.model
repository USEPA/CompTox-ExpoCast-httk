States = {
Agutlumen,  # Amount of the compound in gut lumen (umol)
Agut,       # ... gut (umol)
Aliver,     # ... liver (umol)
Aven,       # ... veins (umol)
Alung,      # ... lung (umol)
Aart,       # ... arterioles (umol)
Arest,      # ... rest of the body (umol)
Akidney,    # ... kidney (umol)
Atubules,   # ... tubules (umol)
Ametabolized, # ... metabolized (umol)
AUC,        # Area under the curve (umol*days)
Ainh,       # Amount of the compound inhaled (umol)
Aexh,       # Amount of the compound exhaled (umol)
Amuc        # Amount of the compound in the mucus (umol)
};

Inputs = {#Cinh # Concentration Inhaled (uM) -- Linakis original input 
 Cinhppmv # ... (ppmv)
};

Outputs = {
Cgut,      # Concentration in the gut (uM)
Cliver,    # ... liver (uM)
Cven,      # ... veins (uM)
Clung,     # ... lung (uM)
Cart,      # ... arterioles (uM)
Crest,     # ... rest of the body (uM)
Ckidney,   # ... kidney (uM)
Cplasma,   # ... plasma (uM)
Aplasma,
Calvppmv,    # Concentration in alveoli (ppmv)
Calv,       # ... (uM) -- Linakis original output
Cendexhppmv, # Concentration exhaled (ppmv)
Cendexh,    # ... (uM) -- Linakis original output
Cmixexhppmv, # Concentration exhaled mixed (ppmv)
Cmixexh,    # ... (uM) -- Linakis original output
Cmuc         # Concentration exhaled mucus (uM)
};

BW = 70;   # Body weight (kg)
Clmetabolismc = 0.203; # Clearance metabolism
vmax = 0;  # Maximal rate of reaction (pmol/min/10^6cells)
km = 1;    # Michaelis-Menten constant (uM)
hematocrit = 0.44; # Hematocritical fraction
kgutabs = 1; # Rate of absorption from gut (1/h)
Kkidney2pu = 0; # Kidney to unbound plasma tissue partition coefficient
Kliver2pu = 0; # Liver to unbound plasma tissue partition coefficient
Krest2pu = 0; # # Rest of body to unbound plasma tissue partition coefficient
Kgut2pu = 0; # Gut to unbound plasma tissue partition coefficient
Klung2pu = 0; # Lung to unbound plasma tissue partition coefficient
Qcardiacc = 4.8; #Cardiac Output (blood, not just plasma, L/h/kg BW^3/4)
Qgfrc = 0.108; # Glomerular Filtration Rate, L/h/kg BW^3/4, volume of fluid filtered from kidney and excreted.
Qgutf = 0.205; # Fraction of cardiac output flowing to the gut.
Qkidneyf = 0.221; #	Fraction of cardiac output flowing to the kidneys.
Qliverf = 0.0536; # Fraction of cardiac output flowing to the liver.
Qlungf = 0; #  Fraction of cardiac output flowing to the liver.
Vartc = 0.0487; # Volume of the arterial blood per kg body weight, L/kg BW.
Vgutc = 0.0158; # Volume of the gut tissue per kg body weight, L/kg BW.
Vkidneyc = 0.00119; # Volume of the kidneys per kg body weight, L/kg BW.
Vliverc = 0.02448; # Volume of the liver per kg body weight, L/kg BW.
Vlungc = 0.00723; # Volume of the lungs per kg body weight, L/kg BW
Vrestc = 0.77654; # Volume of the rest of the body per kg body weight, L/kg BW
Vvenc = 0.0487; # Volume of the veinous blood per kg body weight, L/kg BW
Fraction_unbound_plasma = 0.0682; # Fraction of chemical in plasma that is not bound.
Rblood2plasma = 0.0; # The ratio of the concentration of the chemical in the blood to the concentration in the plasma from available_rblood2plasma.
Clmetabolism = 0.0; # 	    Hepatic Clearance, L/h
Qcardiac = 0.0; #Cardiac Output (L/d) NOTE change to days from hours
Qgfr = 0.0; # Glomerular Filtration Rate L/d
Qgut = 0.0; # Blood (not plasma) flow to gut L/d
Qkidney = 0.0; # Blood flow to kidney (L/d)
Qliver = 0.0; # Blood flow to kidney (L/d)
Qlung = 0.0; # Blood flow to lung (L/d)
Qrest = 0.0; # Blood flow to rest of body (L/d)
Vart = 0.0;  # Volume of the arterial blood (L)
Vgut = 0.0; # Volume of gut tissue (L)
Vkidney = 0.0; # Volume of kidney tissue (L)
Vliver = 0.0; # Volume of liver tissue (L)
Vlung = 0.0; # Volume of lung tissue (L)
Vrest = 0.0; # Volume of rest of body (L) 
Vven = 0.0; # Volume of venous blood (L)
Qalvc = 0.0; # Alveolar ventilation rate (L/h/kg BW^3/4)  
Qalv = 0.0; # Alveolar ventilation rate (L/d) 
Kblood2air = 0.0; # blood-to-air partition coefficient 
InhMag = 0.0; # Unused parameter
Period = 0.0; # Time between exposure events (h)
Exposure = 0.0; # External chemical exposure (user input units)
kUrtc = 11.0; # clearance to the upper respiratory tract (L/h/kg^0.75)
kUrt = 0.0; # clearance to the upper respiratory tract (L/d)
Kmuc2air = 0.0; # mucus-to-air partition coefficients
Vmucc = 0.0001; # Unscaled mucosal volume (L/kg)
Vmuc = 0.0; # Mucosal volume (L)
Vmax = 0.0; # Maximum metabolism rate (mg/h)
Km = 1.0; # Michaelis-Menten Coefficient (mg/L)





Initialize{
kgutabs = kgutabs * 24;
Clmetabolism = Clmetabolismc * 24 * BW;
Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
Qgfr = Qgfrc * pow(BW, 0.75) * 24;
Qgut = Qcardiac * Qgutf;
Qkidney = Qcardiac * Qkidneyf;
Qliver = Qcardiac * Qliverf;
Qlung = Qcardiac * Qlungf;
Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qlung);
Vart = Vartc * BW;
Vgut = Vgutc * BW;
Vkidney = Vkidneyc * BW;
Vliver = Vliverc * BW;
Vlung = Vlungc * BW;
Vrest = Vrestc * BW;
Vven = Vvenc * BW;
Qalv = Qalvc * 24 * pow(BW,0.75);
#Qalv = Qalv * 24 * pow(BW,0.75); # old version from the paper which is incorrect (Qalv - whole body; Qalvc - scaled per unit kg body weight)
kUrt = fmin(kUrtc,Qalvc) * pow(BW,0.75) * 24; #note the IPA model uses min(kUrtC, QalvC));
Vmuc = Vmucc * BW;
Vmax = vmax * 60 * 24;
Km = km;
}

Dynamics{
Cinh = Cinhppmv/24.45;     # Concentration inhaled (uM) - default input is ppmv
Cgut = Agut / Vgut;       # Concentration in the gut
Cliver = Aliver / Vliver; # ... liver 
Cven = Aven / Vven;       # ... veins (uM)
Clung = Alung / Vlung;    # ... lung
Cart = Aart / Vart;       # ... arterioles
Crest = Arest / Vrest;    # ... <>
Ckidney = Akidney / Vkidney; # ... kidney
#Clungart = (Qcardiac * Cven + Qalv * Cinh) / (Qcardiac + Qalv / Kblood2air); # ... lung arterioles
Cplasma = Aven / Vven / Rblood2plasma; # ... plasma (uM)
Aplasma = Aven / Rblood2plasma * (1 - hematocrit); # (umols)
Calv = Cart/Kblood2air;   # Concentration in the alveoli (uM)
Calvppmv = Calv * 24.45;  # ... (ppmv)
Cendexh = ((Qalv * Calv) + kUrt * ((Cmuc/Kmuc2air) - Calv))/Qalv; # Concentration exhaled (uM)
Cendexhppmv = Cendexh * 24.45;        # ... (ppmv)
Cmixexh = 0.7 * Cendexh + 0.3 * Cinh; # Concentration exhaled mix (uM)
Cmixexhppmv = Cmixexh * 24.45;        # ... (ppmv)
Cmuc = Amuc/Vmuc;            # Concentration in mucus

 

dt (Agutlumen) = - kgutabs * Agutlumen;
dt (Agut) = kgutabs * Agutlumen + Qgut * (Cart - Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
dt (Aliver) = Qliver * Cart + Qgut * Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Vmax * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu);
dt (Aven) = ((Qliver + Qgut) * Cliver / Kliver2pu + Qkidney * Ckidney / Kkidney2pu + Qrest * Crest / Krest2pu + Qlung * Clung / Klung2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Cven;
dt (Alung) = Qlung * (Cart - Clung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
dt (Aart) = (Qcardiac * (Cven - Cart)) + (Qalv * (Cinh - Calv)) - (kUrt * (Cinh - (Cmuc/Kmuc2air)));
dt (Arest) = Qrest * (Cart - Crest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
dt (Akidney) = Qkidney * Cart - Qkidney * Ckidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Ckidney / Kkidney2pu;
dt (Atubules) = Qgfr * Ckidney / Kkidney2pu;
dt (Ametabolized) = Clmetabolism * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma + Vmax * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu) ;
dt (AUC) = Cven / Rblood2plasma;
dt (Ainh) = (Qalv * (Calv - Cinh)) + kUrt * ((Cmuc/Kmuc2air) - Calv);
dt (Aexh) = (Qalv * Calv) + kUrt * ((Cmuc/Kmuc2air) - Calv);
dt (Amuc) = (kUrt * (Cinh - (Cmuc/Kmuc2air))) - (kUrt * ((Cmuc/Kmuc2air) - Calv));

}
End.

