States = {
Agutlumen;
Agut;
Aliver;
Aven;
Alung;
Aart;
Arest;
Akidney;
Atubules;
Ametabolized;
AUC;
};

Inputs = {
Cinh;
};

Outputs = {
Cgut;
Cliver;
Cven;
Clung;
Cart;
Crest;
Ckidney;
Cplasma;
Aplasma;
Clungart;
Cgas;
Caerosol;



};

BW = 70;
Clmetabolismc = 0.203;
hematocrit = 0.44;
kgutabs = 1;
Kkidney2pu = 0;
Kliver2pu = 0;
Krest2pu = 0;
Kgut2pu = 0;
Klung2pu = 0;
Qcardiacc = 4.8;
Qgfrc = 0.108;
Qgutf = 0.205;
Qkidneyf = 0.221;
Qliverf = 0.0536;
Qlungf = 0;
Vartc = 0.0487;
Vgutc = 0.0158;
Vkidneyc = 0.00119;
Vliverc = 0.02448;
Vlungc = 0.00723;
Vrestc = 0.77654;
Vvenc = 0.0487;
Fraction_unbound_plasma = 0.0682;
Rblood2plasma = 0.0;
Clmetabolism = 0.0;
Qcardiac = 0.0;
Qgfr = 0.0;
Qgut = 0.0;
Qkidney = 0.0;
Qliver = 0.0;
Qlung = 0.0;
Qrest = 0.0;
Vart = 0.0;
Vgut = 0.0;
Vkidney = 0.0;
Vliver = 0.0;
Vlung = 0.0;
Vrest = 0.0;
Vven = 0.0;
Vdot = 0;
Qalv = 0;
Kblood2air = 0;
Fdeposited = 0;
Fds = 0;
DEalv = 0;
Fgas = 0;




Initialize{
kgutabs = kgutabs * 24;
Clmetabolism = Clmetabolismc * 24 * BW;
Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
Qgfr = Qgfrc * pow(BW, 0.75) * 24;
Qgut = Qcardiac * Qgutf;
Qkidney = Qcardiac * Qkidneyf;
Qliver = Qcardiac * Qliverf;
Qlung = Qcardiac * Qlungf;
Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qlung);
Vart = Vartc * BW;
Vgut = Vgutc * BW;
Vkidney = Vkidneyc * BW;
Vliver = Vliverc * BW;
Vlung = Vlungc * BW;
Vrest = Vrestc * BW;
Vven = Vvenc * BW;
Vdot = Vdot * 24 * pow(BW,0.75);
Qalv = Vdot * (1 - Fds);
}

Dynamics{
Cgut = Agut / Vgut;
Cliver = Aliver / Vliver;
Cven = Aven / Vven;
Clung = Alung / Vlung;
Cart = Aart / Vart;
Crest = Arest / Vrest;
Ckidney = Akidney / Vkidney;
Clungart = (Qcardiac * Cven + Qalv * Cgas) / (Qcardiac + Qalv / Kblood2air);
Cplasma = Aven / Vven / Rblood2plasma;
Aplasma = Aven / Rblood2plasma * (1 - hematocrit);
Cgas = Cinh * Fgas;
Caerosol = Cinh * (1 - Fgas);

 

dt (Agutlumen) = - kgutabs * Agutlumen + Vdot * Fdeposited * Caerosol;
dt (Agut) = kgutabs * Agutlumen + Qgut * (Cart - Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
dt (Aliver) = Qliver * Cart + Qgut * Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Cliver / Kliver2pu;
dt (Aven) = ((Qliver + Qgut) * Cliver / Kliver2pu + Qkidney * Ckidney / Kkidney2pu + Qrest * Crest / Krest2pu + Qlung * Clung / Klung2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Cven;
dt (Alung) = Qlung * (Cart - Clung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
dt (Aart) = Qcardiac * (Clungart - Cart) + Vdot * Caerosol * DEalv;
dt (Arest) = Qrest * (Cart - Crest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
dt (Akidney) = Qkidney * Cart - Qkidney * Ckidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Ckidney / Kkidney2pu;
dt (Atubules) = Qgfr * Ckidney / Kkidney2pu;
dt (Ametabolized) = Clmetabolism * Cliver / Kliver2pu;
dt (AUC) = Cven / Rblood2plasma;
}