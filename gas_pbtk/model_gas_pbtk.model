States = {
Agutlumen,  # Amount of the compound in gut lumen (umol)
Agut,       # ... gut (umol)
Aliver,     # ... liver (umol)
Aven,       # ... veins (umol)
Alung,      # ... lung (umol)
Aart,       # ... arterioles (umol)
Arest,      # ... rest of the body (umol)
Akidney,    # ... kidney (umol)
Atubules,   # ... tubules (umol)
Ametabolized, # ... metabolized (umol)
AUC,        # Area under the curve (umol*days)
Ainh,       # Amount of the compound inhaled (umol)
Aexh,       # Amount of the compound exhaled (umol)
Amuc        # Amount of the compound in the mucus (umol)
};

Inputs = {Cinh # Concentration Inhaled (uM) -- Linakis original input 
# Cinhppmv # ... (ppmv)
};

Outputs = {
Cgut,      # Concentration in the gut (uM)
Cliver,    # ... liver (uM)
Cven,      # ... veins (uM)
Clung,     # ... lung (uM)
Cart,      # ... arterioles (uM)
Crest,     # ... rest of the body (uM)
Ckidney,   # ... kidney (uM)
Cplasma,   # ... plasma (uM)
Aplasma,
#Calvppmv,    # Concentration in alveoli (ppmv)
Calv,       # ... (uM) -- Linakis original output
#Cendexhppmv, # Concentration exhaled (ppmv)
Cendexh,    # ... (uM) -- Linakis original output
#Cmixexhppmv, # Concentration exhaled mixed (ppmv)
Cmixexh,    # ... (uM) -- Linakis original output
Cmuc         # Concentration exhaled mucus (uM)
};

BW = 70;   # Body weight
Clmetabolismc = 0.203; # Clearance metabolism
vmax = 0;  # Maximal rate of reaction (pmol/min/10^6cells)
km = 1;    # Michaelis-Menten constant (uM)
hematocrit = 0.44;
kgutabs = 1;
Kkidney2pu = 0;
Kliver2pu = 0;
Krest2pu = 0;
Kgut2pu = 0;
Klung2pu = 0;
Qcardiacc = 4.8;
Qgfrc = 0.108;
Qgutf = 0.205;
Qkidneyf = 0.221;
Qliverf = 0.0536;
Qlungf = 0;
Vartc = 0.0487;
Vgutc = 0.0158;
Vkidneyc = 0.00119;
Vliverc = 0.02448;
Vlungc = 0.00723;
Vrestc = 0.77654;
Vvenc = 0.0487;
Fraction_unbound_plasma = 0.0682;
Rblood2plasma = 0.0;
Clmetabolism = 0.0;
Qcardiac = 0.0;
Qgfr = 0.0;
Qgut = 0.0;
Qkidney = 0.0;
Qliver = 0.0;
Qlung = 0.0;
Qrest = 0.0;
Vart = 0.0;
Vgut = 0.0;
Vkidney = 0.0;
Vliver = 0.0;
Vlung = 0.0;
Vrest = 0.0;
Vven = 0.0;
Qalvc = 0.0;
Qalv = 0;
Kblood2air = 0;
InhMag = 0;
Period = 0;
Exposure = 0;
kUrtc = 11.0; # clearance to the URT (L/h/kg^0.75)
kUrt = 0;
Kmuc2air = 0;
Vmucc = 0.0001;
Vmuc = 0.0;
Vmax = 0;     # Maximal rate of reaction (pmol/min/10^6cells)
Km = 1;





Initialize{
kgutabs = kgutabs * 24;
Clmetabolism = Clmetabolismc * 24 * BW;
Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
Qgfr = Qgfrc * pow(BW, 0.75) * 24;
Qgut = Qcardiac * Qgutf;
Qkidney = Qcardiac * Qkidneyf;
Qliver = Qcardiac * Qliverf;
Qlung = Qcardiac * Qlungf;
Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qlung);
Vart = Vartc * BW;
Vgut = Vgutc * BW;
Vkidney = Vkidneyc * BW;
Vliver = Vliverc * BW;
Vlung = Vlungc * BW;
Vrest = Vrestc * BW;
Vven = Vvenc * BW;
Qalv = Qalvc * 24 * pow(BW,0.75);
#Qalv = Qalv * 24 * pow(BW,0.75); # old version from the paper which is incorrect (Qalv - whole body; Qalvc - scaled per unit kg body weight)
kUrt = fmin(kUrtc,Qalv/24/pow(BW,0.75)) * pow(BW,0.75) * 24; #note the IPA model uses min(kUrtC, QalvC));
Vmuc = Vmucc * BW;
Vmax = vmax * 60 * 24;
Km = km;
}

Dynamics{
#Cinh = Cinhppmv/24.45;     # Concentration inhaled (uM) - default input is ppmv
Cgut = Agut / Vgut;       # Concentration in the gut
Cliver = Aliver / Vliver; # ... liver 
Cven = Aven / Vven;       # ... veins (uM)
Clung = Alung / Vlung;    # ... lung
Cart = Aart / Vart;       # ... arterioles
Crest = Arest / Vrest;    # ... <>
Ckidney = Akidney / Vkidney; # ... kidney
#Clungart = (Qcardiac * Cven + Qalv * Cinh) / (Qcardiac + Qalv / Kblood2air); # ... lung arterioles
Cplasma = Aven / Vven / Rblood2plasma; # ... plasma (uM)
Aplasma = Aven / Rblood2plasma * (1 - hematocrit); # (umols)
Calv = Cart/Kblood2air;   # Concentration in the alveoli (uM)
Calvppmv = Calv * 24.45;  # ... (ppmv)
Cendexh = ((Qalv * Calv) + kUrt * ((Cmuc/Kmuc2air) - Calv))/Qalv; # Concentration exhaled (uM)
Cendexhppmv = Cendexh * 24.45;        # ... (ppmv)
Cmixexh = 0.7 * Cendexh + 0.3 * Cinh; # Concentration exhaled mix (uM)
Cmixexhppmv = Cmixexh * 24.45;        # ... (ppmv)
Cmuc = Amuc/Vmuc;            # Concentration in mucus

 

dt (Agutlumen) = - kgutabs * Agutlumen;
dt (Agut) = kgutabs * Agutlumen + Qgut * (Cart - Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
dt (Aliver) = Qliver * Cart + Qgut * Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Vmax * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu);
dt (Aven) = ((Qliver + Qgut) * Cliver / Kliver2pu + Qkidney * Ckidney / Kkidney2pu + Qrest * Crest / Krest2pu + Qlung * Clung / Klung2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Cven;
dt (Alung) = Qlung * (Cart - Clung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
dt (Aart) = (Qcardiac * (Cven - Cart)) + (Qalv * (Cinh - Calv)) - (kUrt * (Cinh - (Cmuc/Kmuc2air)));
dt (Arest) = Qrest * (Cart - Crest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
dt (Akidney) = Qkidney * Cart - Qkidney * Ckidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Ckidney / Kkidney2pu;
dt (Atubules) = Qgfr * Ckidney / Kkidney2pu;
dt (Ametabolized) = Clmetabolism * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma + Vmax * Cliver / Kliver2pu / (Km + Cliver / Kliver2pu) ;
dt (AUC) = Cven / Rblood2plasma;
dt (Ainh) = (Qalv * (Calv - Cinh)) + kUrt * ((Cmuc/Kmuc2air) - Calv);
dt (Aexh) = (Qalv * Calv) + kUrt * ((Cmuc/Kmuc2air) - Calv);
dt (Amuc) = (kUrt * (Cinh - (Cmuc/Kmuc2air))) - (kUrt * ((Cmuc/Kmuc2air) - Calv));

}
End.

