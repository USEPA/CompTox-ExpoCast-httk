States = {
Agutlumen,
Agut,
Aliver,
Aven,
Alung,
Aart,
Arest,
Akidney,
Atubules,
Ametabolized,
AUC,
};

Outputs = {
Cgut,
Cliver,
Cven,
Clung,
Cart,
Crest,
Ckidney,
Cplasma,
Aplasma,
};

BW = 70;
Clmetabolismc = 0.203;
hematocrit = 0.44;
kgutabs = 1;
Kkidney2pu = 0;
Kliver2pu = 0;
Krest2pu = 0;
Kgut2pu = 0;
Klung2pu = 0;
Qcardiacc = 4.8;
Qgfrc = 0.108;
Qgutf = 0.205;
Qkidneyf = 0.221;
Qliverf = 0.0536;
Vartc = 0.0487;
Vgutc = 0.0158;
Vkidneyc = 0.00119;
Vliverc = 0.02448;
Vlungc = 0.00723;
Vrestc = 0.77654;
Vvenc = 0.0487;
Fraction_unbound_plasma = 0.0682;
Rblood2plasma = 0.0;
Clmetabolism = 0.0;
Qcardiac = 0.0;
Qgfr = 0.0;
Qgut = 0.0;
Qkidney = 0.0;
Qliver = 0.0;
Qrest = 0.0;
Vart = 0.0;
Vgut = 0.0;
Vkidney = 0.0;
Vliver = 0.0;
Vlung = 0.0;
Vrest = 0.0;
Vven = 0.0;



Initialize{
kgutabs = kgutabs * 24;
Clmetabolism = Clmetabolismc * 24 * BW;
Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
Qgfr = Qgfrc * pow(BW, 0.75) * 24;
Qgut = Qcardiac * Qgutf;
Qkidney = Qcardiac * Qkidneyf;
Qliver = Qcardiac * Qliverf;
Qrest = Qcardiac - (Qgut + Qkidney + Qliver);
Vart = Vartc * BW;
Vgut = Vgutc * BW;
Vkidney = Vkidneyc * BW;
Vliver = Vliverc * BW;
Vlung = Vlungc * BW;
Vrest = Vrestc * BW;
Vven = Vvenc * BW;
}

Dynamics{
Cgut = Agut / Vgut;
Cliver = Aliver / Vliver;
Cven = Aven / Vven;
Clung = Alung / Vlung;
Cart = Aart / Vart;
Crest = Arest / Vrest;
Ckidney = Akidney / Vkidney;
Cplasma = Aven / Vven / Rblood2plasma;
Aplasma = Aven / Rblood2plasma * (1 - hematocrit);

 

dt (Agutlumen) = - kgutabs * Agutlumen;
dt (Agut) = kgutabs * Agutlumen + Qgut * (Aart / Vart - Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
dt (Aliver) = Qliver * Aart / Vart + Qgut * Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Aliver / Vliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Aliver / Vliver / Kliver2pu;
dt (Aven) = ((Qliver + Qgut) * Aliver / Vliver / Kliver2pu + Qkidney * Akidney / Vkidney / Kkidney2pu + Qrest * Arest / Vrest / Krest2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Aven / Vven;
dt (Alung) = Qcardiac * (Aven / Vven - Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
dt (Aart) = Qcardiac * (Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma - Aart / Vart );
dt (Arest) = Qrest * (Aart/Vart - Arest/Vrest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
dt (Akidney) = Qkidney * Aart / Vart - Qkidney * Akidney / Vkidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Akidney / Vkidney / Kkidney2pu;
dt (Atubules) = Qgfr * Akidney / Vkidney / Kkidney2pu;
dt (Ametabolized) = Clmetabolism * Aliver / Vliver / Kliver2pu ;
dt (AUC) = Aven / Vven / Rblood2plasma;

}
End.
