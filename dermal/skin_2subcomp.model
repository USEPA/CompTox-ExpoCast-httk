States = {
	Agutlumen,			# Amount of the compound in gut lumen (umol)
	Agut,				# ... gut (umol)
	Aliver,				# ... liver (umol)
	Aven,				# ... veins (umol)
	Alung,				# ... lung (umol)
	Aart,				# ... arterioles (umol)
	Arest,				# ... rest of the body (umol)
	Akidney,			# ... kidney (umol)
	Atubules,			# ... tubules (umol)
	Ametabolized,			# ... metabolized (umol)
	AUC,				# Area under the curve (umol*days)
	Astratum_corneum_exposed,	# Amount in exposed stratum corneum (umol)
	Astratum_corneum_unexposed,	# Amount in unexposed stratum corneum (umol)
	Acomposite_dermal_exposed,	# Amount in exposed dermal layer (umol)
	Acomposite_dermal_unexposed,	# Amount in unexposed dermal layer (umol)
	Amedia,				# ?
};

Inputs = {
	forcing,
	switch,
};

Outputs = {
	Cgut,				# Concentration in guy (uM)
	Cliver,				# ... liver (uM)
	Cven,				# ... veins (uM)
	Clung,				# ... lung (uM)
	Cart,				# ... arterioles (uM)
	Crest,             		# ... rest of the body (uM)
	Ckidney,			# ... kidney (uM)
	Cplasma,			# ... plasma (uM)
	Aplasma,			# ??
	Cstratum_corneum_exposed,	# Concentration in exposed stratum corneum (uM)
	Cstratum_corneum_unexposed,	# Concentration in unexposed stratum corneum (uM)
	Ccomposite_dermal_exposed,	# Concentration in exposed dermal layer (uM)
	Ccomposite_dermal_unexposed,	# Concentration in unexposed dermal layer (uM)
	Cmedia,				# ?
};


skin_depth = 0;
Fskin_depth_sc = 0;
Fskin_depth_cd = 0;
Pmedia2sc = 0;
Psc2cd = 0;
V0 = 0;
Fskin_exposed = 0;
totalSA = 0;
SA_exposed = 0;
BW = 70;
Clmetabolismc = 0.203;
hematocrit = 0.44;
kgutabs = 1;

# Particion coefficient of ... (N/A)
Ksc2media = 0;		
Ksc2cd = 0;
Kcd2pu = 0;
Kkidney2pu = 0;
Kliver2pu = 0;
Krest2pu = 0;
Kgut2pu = 0;
Klung2pu = 0;

Qcardiacc = 4.8;
Qgfrc = 0.108;
Qcomposite_dermalc = 0;
Qgutc = 0;
Qkidneyc = 0;
Qliverc = 0;

# Volume of the ... per kg of body weight (L/kg BW)
Vartc = 0;
Vgutc = 0;
Vkidneyc = 0;
Vliverc = 0;
Vlungc = 0;
Vrestc = 0;
Vvenc = 0;
Vskinc = 0;
Vstratum_corneumc = 0;
Vcomposite_dermalc = 0;

Fraction_unbound_plasma = 0.0;
Rblood2plasma = 0.0;
Clmetabolism = 0.0;
Qcardiac = 0.0;
Qcomposite_dermal = 0;
Qcomposite_dermal_exposed = 0;
Qcomposite_dermal_unexposed = 0;
Qgfr = 0.0;

# Blood flow to ... (L/d)
Qgut = 0.0;
Qkidney = 0.0;
Qliver = 0.0;
Qrest = 0.0;

# Volume of ... (L)
Vart = 0.0;
Vgut = 0.0;
Vkidney = 0.0;
Vliver = 0.0;
Vlung = 0.0;
Vrest = 0.0;
Vven = 0.0;
Vskin = 0;
Vstratum_corneum = 0;
Vstratum_corneum_exposed = 0;
Vstratum_corneum_unexposed = 0;
Vcomposite_dermal = 0;
Vcomposite_dermal_exposed = 0;
Vcomposite_dermal_unexposed = 0;


Initialize{
	kgutabs = kgutabs * 24;
	Clmetabolism = Clmetabolismc * 24 * BW;
	Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
	Qgfr = Qgfrc * pow(BW, 0.75) * 24;
	Qgut = Qcardiac * Qgutc;
	Qkidney = Qcardiac * Qkidneyc;
	Qliver = Qcardiac * Qliverc;
	SA_exposed = Fskin_exposed * totalSA;
	Vskin = BW * Vskinc;
	Vskin_exposed = SA_exposed * skin_depth * 0.001;
	Vskin_unexposed = Vskin - Vskin_exposed;
	Vstratum_corneum = Vskin * Fskin_depth_sc;
	Vstratum_corneum_exposed = Vstratum_corneum * Fskin_exposed;
	Vstratum_corneum_unexposed = Vstratum_corneum * (1 - Fskin_exposed);
	Vcomposite_dermal = Vskin * Fskin_depth_cd;
	Vcomposite_dermal_exposed = Vcomposite_dermal * Fskin_exposed;
	Vcomposite_dermal_unexposed = Vcomposite_dermal * (1 - Fskin_exposed);
	Qcomposite_dermal = Qcardiac * Qcomposite_dermalc;
	Qcomposite_dermal_unexposed = Qcomposite_dermal * (1 - Vskin_exposed / Vskin);
	Qcomposite_dermal_exposed = Qcomposite_dermal * Vskin_exposed / Vskin;
	Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qcomposite_dermal);
	Vart = Vartc * BW;
	Vgut = Vgutc * BW;
	Vkidney = Vkidneyc * BW;
	Vliver = Vliverc * BW;
	Vlung = Vlungc * BW;
	Vrest = Vrestc * BW;
	Vven = Vvenc * BW;
}

Dynamics{
	Cgut = Agut / Vgut;
	Cliver = Aliver / Vliver;
	Cven = Aven / Vven;
	Clung = Alung / Vlung;
	Cart = Aart / Vart;
	Crest = Arest / Vrest;
	Ckidney = Akidney / Vkidney;
	Cplasma = Aven / Vven / Rblood2plasma;
	Aplasma = Aven / Rblood2plasma * (1 - hematocrit);
	Cstratum_corneum_unexposed = Astratum_corneum_unexposed / Vstratum_corneum_unexposed;
	Cstratum_corneum_exposed = Astratum_corneum_exposed / Vstratum_corneum_exposed;
	Ccomposite_dermal_unexposed = Acomposite_dermal_unexposed / Vcomposite_dermal_unexposed;
	Ccomposite_dermal_exposed = Acomposite_dermal_exposed / Vcomposite_dermal_exposed;
	Vmedia = V0 * forcing;
	Cmedia = Amedia / Vmedia;

	dt (Agutlumen) = - kgutabs * Agutlumen;
	dt (Amedia) = - Pmedia2sc * SA_exposed * 24 * 0.001 * (Cmedia - Cstratum_corneum_exposed / Ksc2media) * switch;
	dt (Agut) = kgutabs * Agutlumen + Qgut * (Aart / Vart - Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
	dt (Aliver) = Qliver * Aart / Vart + Qgut * Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Aliver / Vliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Aliver / Vliver / Kliver2pu;
	dt (Aven) = ((Qliver + Qgut) * Aliver / Vliver / Kliver2pu + Qkidney * Akidney / Vkidney / Kkidney2pu + Qrest * Arest / Vrest / Krest2pu + Qcomposite_dermal_unexposed * Ccomposite_dermal_unexposed / Kcd2pu + Qcomposite_dermal_exposed * Ccomposite_dermal_exposed / Kcd2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Aven / Vven;
	dt (Alung) = Qcardiac * (Aven / Vven - Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
	dt (Aart) = Qcardiac * (Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma - Aart / Vart );
	dt (Arest) = Qrest * (Aart/Vart - Arest/Vrest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
	dt (Akidney) = Qkidney * Aart / Vart - Qkidney * Akidney / Vkidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Akidney / Vkidney / Kkidney2pu;
	dt (Atubules) = Qgfr * Akidney / Vkidney / Kkidney2pu;
	dt (Ametabolized) = Clmetabolism * Aliver / Vliver / Kliver2pu ;
	dt (AUC) = Aven / Vven / Rblood2plasma;
	dt (Astratum_corneum_exposed) = Pmedia2sc * SA_exposed * 24 * 0.001 * (Cmedia - Cstratum_corneum_exposed / Ksc2media) * switch + Psc2cd * SA_exposed * 24 * 0.001 * (Ccomposite_dermal_exposed - Cstratum_corneum_exposed / Ksc2cd)  ;
	dt (Astratum_corneum_unexposed) = Psc2cd * (totalSA - SA_exposed) * 24 * 0.001 * (Ccomposite_dermal_unexposed - Cstratum_corneum_unexposed / Ksc2cd);
	dt (Acomposite_dermal_exposed) = Qcomposite_dermal_exposed * (Cart - Ccomposite_dermal_exposed * Rblood2plasma / Kcd2pu / Fraction_unbound_plasma) + Psc2cd * SA_exposed * 24 * 0.001 * (Cstratum_corneum_exposed / Ksc2cd - Ccomposite_dermal_exposed);
	dt (Acomposite_dermal_unexposed) = Qcomposite_dermal_unexposed * (Cart - Ccomposite_dermal_unexposed * Rblood2plasma / Kcd2pu / Fraction_unbound_plasma) + Psc2cd * (totalSA - SA_exposed) * 24 * 0.001 * (Cstratum_corneum_unexposed / Ksc2cd - Ccomposite_dermal_unexposed);
}

End.