#---------------------------------------------------------------------------------------------
# STATE VARIABLES for the model (for which ODEs are provided).

States = {
     Agutlumen,		# Amount of the compound in gut lumen (umol)
     Agut,		# ... gut (umol)
     Aliver,		# ... liver (umol)
     Aven,		# ... veins (umol)
     Alung,		# ... lung (umol)
     Aart,		# ... arterioles (umol)
     Arest,		# ... rest of the body (umol)
     Akidney,		# ... kidney (umol)
     Atubules,		# ... tubules (umol)
     Ametabolized,	# ... metabolized (umol)
     AUC,		# Area under the curve (umol*days)
     Askinexposed,	# Amount in exposed skin (umol)
     Askin,		# Amount ??
     Amedia,		# Amount at skin surface (umol)
};

# End of STATE VARIABLES
#---------------------------------------------------------------------------------------------

#---------------------------------------------------------------------------------------------
# INPUT VARIABLES for the model (which are independent of other variables, and which may vary
# in time).

Inputs = {
     forcing,
     switch,
};

# End of INPUT VARAIBLES
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# OUTPUT VARIABLES for the model (which can be obtained at any point in time as analytic 
# functions of state variables, inputs, and parameters).

Outputs = {
     Cgut,		# Concentration in guy (uM)
     Cliver,		# ... liver (uM)
     Cven,		# ... veins (uM)
     Clung,		# ... lung (uM)
     Cart,		# ... arterioles (uM)
     Crest,		# ... rest of the body (uM)
     Ckidney,		# ... kidney (uM)
     Cplasma,		# ... plasma (uM)
     Aplasma,		# ??
     Cskinexposed, 	# Concentration in exposed skin (uM) ?
     Cskin,		# ??
     Cmedia,		# Concentration at the skin surface (uM) ?
};

# End of OUTPUT VARIABLES
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# PARAMETERS for the model (which are independent of time).

depth = 0;
V0 = 0;
Fskinexposed = 0;
totalSA = 0;
SA = 0;				# Surface area exposed
Kp = 0;				# 
Kskin2media = 0;		# skin:media partition coefficient (N/A)
BW = 70;			# Body weight (kg)
Clmetabolismc = 0.203;
hematocrit = 0.44;
kgutabs = 1;

# Partition Coefficient of ... (N/A)
Kkidney2pu = 0;	# Kidney to unbound plasma tissue 
Kliver2pu = 0;	# Liver to unbound plasma tissue 
Krest2pu = 0;	# Rest of body to unbound plasma tissue 
Kgut2pu = 0;	# Gut to unbound plasma tissue
Klung2pu = 0;	# Lung to unbound plasma tissue
Kskin2pu = 0;	# Skin to unbound plasma tissue

Qcardiacc = 4.8;	# Cardiac Output (blood, not just plasma, L/h/kg BW^0.75)
Qgfrc = 0.108;		# Glomerular Filtration Rate, volume of fluid filtered from kidney and excreted (L/h/kg BW^0.75)

# Fraction of cardiac output flowing to ... (N/A)
Qskinf = 0;		# Skin
Qskin = 0;		# ?
Qskinexposed = 0;	# ?
Qgutf = 0;		# Gut
Qkidneyf = 0;		# Kidney
Qliverf = 0;		# Liver

# Volume of the ... per kg of body weight (L/kg BW)
Vartc = 0;		# Arterial blood
Vgutc = 0;		# Gut tissue
Vkidneyc = 0;		# Kidneys
Vliverc = 0;		# Liver
Vlungc = 0;		# Lung
Vrestc = 0;		# Rest of the body
Vvenc = 0;		# Veinous blood
Vskinc = 0;		# ?
Vskin = 0;		# ?
Vskinexposed = 0;	# ?

Fraction_unbound_plasma = 0.0;	# Fraction of chemical in plasma that is not bound. (N/A)
Rblood2plasma = 0.0;		# Ratio of the concentration of the chemical in the blood to the concentration in the plasma from availabel_rblood2plasma. (N/A)
Clmetabolism = 0.0;		# Hepatic Clearance (L/h)
Qcardiac = 0.0;			# Cardiac output (L/d) NOTE change to days from hours
Qgfr = 0.0;			# Glomerular Filtration Rate (L/d)

# Blood flow to ... (L/d)
Qgut = 0.0;		# Gut
Qkidney = 0.0;		# Kidney
Qliver = 0.0;		# Liver
Qrest = 0.0;		# Rest of the body

# Volume of ... (L)
Vart = 0.0;		# Arterial blood
Vgut = 0.0;		# Gut tissue
Vkidney = 0.0;		# Kidney tissue
Vliver = 0.0;		# Liver tissue
Vlung = 0.0;		# Lung tissue
Vrest = 0.0;		# Rest of body
Vven = 0.0;		# Venous blood

# End of PARAMETERS
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# MODEL INITIALIZATION section.

Initialize{
     kgutabs = kgutabs * 24;
     Clmetabolism = Clmetabolismc * 24 * BW;
     Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
     Qgfr = Qgfrc * pow(BW, 0.75) * 24;
     Qgut = Qcardiac * Qgutf;
     Qkidney = Qcardiac * Qkidneyf;
     Qliver = Qcardiac * Qliverf;
     SA = Fskinexposed * totalSA;
     Vskinexposed = SA * depth * 0.001;
     Vskin = Vskinc * BW - Vskinexposed;
     Qskin = Qcardiac * Qskinf * (1 - Vskinexposed / (Vskinc * BW));			# Blood flow to skin (1/time)
     Qskinexposed = Qcardiac * Qskinf * Vskinexposed / (Vskinc * BW);
     Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qskin + Qskinexposed);
     Vart = Vartc * BW;
     Vgut = Vgutc * BW;
     Vkidney = Vkidneyc * BW;
     Vliver = Vliverc * BW;
     Vlung = Vlungc * BW;
     Vrest = Vrestc * BW;
     Vven = Vvenc * BW;
}

# End of MODEL INITIALIZATION
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# DYNAMICS section.

Dynamics{
     Cgut = Agut / Vgut;
     Cliver = Aliver / Vliver;
     Cven = Aven / Vven;
     Clung = Alung / Vlung;
     Cart = Aart / Vart;
     Crest = Arest / Vrest;
     Ckidney = Akidney / Vkidney;
     Cplasma = Aven / Vven / Rblood2plasma;
     Aplasma = Aven / Rblood2plasma * (1 - hematocrit);
     Cskin = Askin / Vskin;
     Cskinexposed = Askinexposed / Vskinexposed;
     Vmedia = V0 * forcing;
     Cmedia = Amedia / Vmedia;

     dt (Agutlumen) = - kgutabs * Agutlumen;
     dt (Amedia) = - Kp * SA * 24 * 0.001 * (Cmedia - Cskinexposed / Kskin2media) * switch;
     dt (Agut) = kgutabs * Agutlumen + Qgut * (Aart / Vart - Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
     dt (Aliver) = Qliver * Aart / Vart + Qgut * Agut / Vgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Aliver / Vliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Aliver / Vliver / Kliver2pu;
     dt (Aven) = ((Qliver + Qgut) * Aliver / Vliver / Kliver2pu + Qkidney * Akidney / Vkidney / Kkidney2pu + Qrest * Arest / Vrest / Krest2pu + Qskin * Cskin / Kskin2pu + Qskinexposed * Cskinexposed / Kskin2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac * Aven / Vven;
     dt (Alung) = Qcardiac * (Aven / Vven - Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
     dt (Aart) = Qcardiac * (Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma - Aart / Vart );
     dt (Arest) = Qrest * (Aart/Vart - Arest/Vrest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
     dt (Akidney) = Qkidney * Aart / Vart - Qkidney * Akidney / Vkidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Akidney / Vkidney / Kkidney2pu;
     dt (Atubules) = Qgfr * Akidney / Vkidney / Kkidney2pu;
     dt (Ametabolized) = Clmetabolism * Aliver / Vliver / Kliver2pu ;
     dt (AUC) = Aven / Vven / Rblood2plasma;
     dt (Askinexposed) = Qskinexposed * (Cart - Cskinexposed * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma) + Kp * SA * 24 * 0.001 * (Cmedia - Cskinexposed / Kskin2media) * switch;
     dt (Askin) = Qskin * (Cart - Cskin * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma);
}
# End of DYNAMICS.
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# CALCULATE OUTPUTS section.

CalcOutputs { }

# End of CALCULATE OUTPUTS.
#---------------------------------------------------------------------------------------------


}