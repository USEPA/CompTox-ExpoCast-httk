#---------------------------------------------------------------------------------------------
# STATE VARIABLES for the model (for which ODEs are provided).

States = {
     Agutlumen,		# Amount of the compound in gut lumen (umol)
     Agut,		# ... gut (umol)
     Aliver,		# ... liver (umol)
     Aven,		# ... veins (umol)
     Alung,		# ... lung (umol)
     Aart,		# ... arterioles (umol)
     Arest,		# ... rest of the body (umol)
     Akidney,		# ... kidney (umol)
     Atubules,		# ... tubules (umol)
     Ametabolized,	# ... metabolized (umol)
     AUC,		# Area under the curve (umol*days)
     Askin_exposed,	# Amount in exposed skin (umol)
     Askin_unexposed,	# Amount in unexposed skin (umol)
     Amedia,		# Amount in media (e.g. lotion, ointment, etc.) (umol)
};

# End of STATE VARIABLES
#---------------------------------------------------------------------------------------------

#---------------------------------------------------------------------------------------------
# INPUT VARIABLES for the model (which are independent of other variables, and which may vary
# in time).

Inputs = {
     Vmedia, # Volume of media (L)
};

# End of INPUT VARAIBLES
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# OUTPUT VARIABLES for the model (which can be obtained at any point in time as analytic 
# functions of state variables, inputs, and parameters).

Outputs = {
     Cgut,		# Concentration in guy (uM)
     Cliver,		# ... liver (uM)
     Cven,		# ... veins (uM)
     Clung,		# ... lung (uM)
     Cart,		# ... arterioles (uM)
     Crest,		# ... rest of the body (uM)
     Ckidney,		# ... kidney (uM)
     Cplasma,		# ... plasma (uM)
     Aplasma,		# ??
     Cskin_exposed, 	# Concentration in exposed skin (uM)
     Cskin_unexposed,	# Concentration in unexposed skin
     Cmedia,		# Concentration in media (e.g. lotion, ointment, etc.) (uM)
};

# End of OUTPUT VARIABLES
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# PARAMETERS for the model (which are independent of time).

skin_depth = 0;
Fskin_exposed = 0;
totalSA = 0;
SA_exposed = 0;			# CALCULATED = totalSA * Fskin_exposed
Kp = 0;				
Kskin2media = 0;		 
BW = 70;			 
Clmetabolismc = 0.0;
hematocrit = 0.0;
kgutabs = 0.0;

# Partition Coefficient of ... to unbound plasma tissue (N/A)
Kkidney2pu = 0;	# Kidney
Kliver2pu = 0;	# Liver
Krest2pu = 0;	# Rest 
Kgut2pu = 0;	# Gut
Klung2pu = 0;	# Lung
Kskin2pu = 0;	# Skin

Qcardiacc = 0;	# Cardiac Output (blood, not just plasma, L/h/kg BW^0.75)
Qgfrc = 0;		# Glomerular Filtration Rate, volume of fluid filtered from kidney and excreted (L/h/kg BW^0.75)

# Fraction of cardiac output flowing to ... (N/A)
Qskinf = 0;		# Skin
Qgutf = 0;		# Gut
Qkidneyf = 0;		# Kidney
Qliverf = 0;		# Liver

# Volume of the ... per kg of body weight (L/kg BW)
Vartc = 0;		# Arterial blood
Vgutc = 0;		# Gut tissue
Vkidneyc = 0;		# Kidneys
Vliverc = 0;		# Liver
Vlungc = 0;		# Lung
Vrestc = 0;		# Rest of the body
Vvenc = 0;		# Veinous blood
Vskinc = 0;		# Skin

Fraction_unbound_plasma = 0.0;	# Fraction of chemical in plasma that is not bound. (N/A)
Rblood2plasma = 0.0;		# Ratio of the concentration of the chemical in the blood to the concentration in the plasma from availabel_rblood2plasma. (N/A)
Clmetabolism = 0.0;		# Hepatic Clearance (L/h)
Qcardiac = 0.0;			# Cardiac output (L/d) NOTE change to days from hours
Qgfr = 0.0;			# Glomerular Filtration Rate (L/d)

# Blood flow to ... (L/d)
Qskin = 0.0;		# Skin (exposed and unexposed)
Qskin_exposed = 0.0;	# Exposed skin
Qskin_unexposed = 0.0;	# Unexposed skin
Qgut = 0.0;		# Gut
Qkidney = 0.0;		# Kidney
Qliver = 0.0;		# Liver
Qrest = 0.0;		# Rest of the body

# Volume of ... (L)
Vart = 0.0;		# Arterial blood
Vgut = 0.0;		# Gut tissue
Vkidney = 0.0;		# Kidney tissue
Vliver = 0.0;		# Liver tissue
Vlung = 0.0;		# Lung tissue
Vrest = 0.0;		# Rest of body
Vven = 0.0;		# Venous blood
Vskin = 0.0;		# Skin (exposed and unexposed)
Vskin_exposed = 0.0;	# Exposed skin
Vskin_unexposed = 0.0; 	# Unexposed skin

# End of PARAMETERS
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# MODEL INITIALIZATION section.

Initialize{
	# Metabolism parameters
	kgutabs = kgutabs * 24;
	Clmetabolism = Clmetabolismc * 24 * BW;
	Qcardiac = Qcardiacc * 24 * pow(BW, 0.75);
	Qgfr = Qgfrc * pow(BW, 0.75) * 24;
	
	# Flow rates
	Qgut = Qcardiac * Qgutf;
	Qkidney = Qcardiac * Qkidneyf;
	Qliver = Qcardiac * Qliverf;
	
	# Surface area of exposed skin
	SA_exposed = Fskin_exposed * totalSA;
	
	# Volumes of skin
	Vskin = Vskinc * BW;
	Vskin_exposed = SA_exposed * skin_depth * 0.001;
	Vskin_unexposed = Vskin - Vskin_exposed;
	
	# Flow rates of skin
	Qskin = Qcardiac * Qskinf; # total skin
	Qskin_unexposed = Qskin * Vskin_unexposed / Vskin;
	Qskin_exposed = Qskin * Vskin_exposed / Vskin;
	Qrest = Qcardiac - (Qgut + Qkidney + Qliver + Qskin);
	
	# Volumes
	Vart = Vartc * BW;
	Vgut = Vgutc * BW;
	Vkidney = Vkidneyc * BW;
	Vliver = Vliverc * BW;
	Vlung = Vlungc * BW;
	Vrest = Vrestc * BW;
	Vven = Vvenc * BW;
}

# End of MODEL INITIALIZATION
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# DYNAMICS section.

Dynamics{
	#Concentrations (uM = umol/L)
	Cgut = Agut / Vgut;
	Cliver = Aliver / Vliver;
	Cven = Aven / Vven;
	Clung = Alung / Vlung;
	Cart = Aart / Vart;
	Crest = Arest / Vrest;
	Ckidney = Akidney / Vkidney;
	
	# Concentration and amount in plasma
	Cplasma = Aven / Vven / Rblood2plasma;
	Aplasma = Aven / Rblood2plasma * (1 - hematocrit);
	
	# Concentrations of skin and media (uM = umol / L).
	Cskin_unexposed = Askin_unexposed / Vskin_unexposed;
	Cskin_exposed = Askin_exposed / Vskin_exposed;
	Cmedia = Amedia / Vmedia;

	# Rate of absorbtion into skin from media (umol / day). This is zero if Amedia is zero (non negative rate).
	R_absorbtion = (Amedia ? Kp * SA_exposed * 24 * 0.001 * (Cmedia - Cskin_exposed / Kskin2media) : 0.0);
	
	#Rate of change of chemical in media (umol / day).
	dt (Amedia) = - R_absorbtion;
	
	# Rate of change of chemical in skin (umol / day).
	dt (Askin_exposed) = Qskin_exposed * (Cart - Cskin_exposed * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma) + R_absorbtion;
	dt (Askin_unexposed) = Qskin_unexposed * (Cart - Cskin_unexposed * Rblood2plasma / Kskin2pu / Fraction_unbound_plasma);
	
	# Rate of change of chemical in rest of body (umol / day).
	dt (Agutlumen) = - kgutabs * Agutlumen;
	dt (Agut) = kgutabs * Agutlumen + Qgut * (Cart - Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma);
	dt (Aliver) = Qliver * Cart + Qgut * Cgut * Rblood2plasma / Kgut2pu / Fraction_unbound_plasma - (Qliver + Qgut) * Cliver / Kliver2pu/ Fraction_unbound_plasma * Rblood2plasma - Clmetabolism * Cliver / Kliver2pu;
	dt (Aven) = ((Qliver + Qgut) * Cliver / Kliver2pu + Qkidney * Ckidney / Kkidney2pu + Qrest * Crest / Krest2pu + Qskin_unexposed * Cskin_unexposed / Kskin2pu + Qskin_exposed * Cskin_exposed / Kskin2pu) * Rblood2plasma / Fraction_unbound_plasma - Qcardiac *Cven;
	dt (Alung) = Qcardiac * (Cven - Clung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma);
	dt (Aart) = Qcardiac * (Alung / Vlung * Rblood2plasma / Klung2pu / Fraction_unbound_plasma - Aart / Vart );
	dt (Arest) = Qrest * (Cart - Arest/Vrest * Rblood2plasma / Krest2pu / Fraction_unbound_plasma);
	dt (Akidney) = Qkidney * Cart - Qkidney * Ckidney / Kkidney2pu * Rblood2plasma / Fraction_unbound_plasma - Qgfr * Ckidney / Kkidney2pu;
	dt (Atubules) = Qgfr * Ckidney / Kkidney2pu;
	dt (Ametabolized) = Clmetabolism * Cliver / Kliver2pu ;
	dt (AUC) = Cven / Rblood2plasma;
}
# End of DYNAMICS.
#---------------------------------------------------------------------------------------------


#---------------------------------------------------------------------------------------------
# CALCULATE OUTPUTS section.

#CalcOutputs { }

# End of CALCULATE OUTPUTS.
#---------------------------------------------------------------------------------------------

End.